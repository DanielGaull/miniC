COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!"\n" ~ ANY)*) }
WHITESPACE = _{ " " | "\n" | "\t" | "\r" }

program = { SOI ~ topLevel* ~ EOI }

// == Top-level stuff ==
topLevel = { module | function | libImport | fileImport | preprocessorDirective | topLevelWithSemicolon }
topLevelWithSemicolon = _{ (functionHeader | typedef | varDec | struct | enum | union) ~ ";" }

function = { functionHeader ~ "{" ~ statement* ~ "}" }
functionHeader = { "extern"? ~ typ ~ identifier ~ "(" ~ paramList? ~ ")" }

struct = { "struct" ~ identifier ~ "{" ~ structInner* ~ "}" }
anonStruct = { "struct" ~ "{" ~ structInner* ~ "}" }
structInner = _{ structVarDec | anonUnion ~ ";" | anonStruct ~ ";" }
structVarDec = { typ ~ identifier ~ ";" }

libImport = { "#include" ~ "<" ~ importInner ~ ">" }
fileImport = { "#include" ~ "\"" ~ importInner ~ "\"" }
importInner = { ("_" | "/" | "." | "\\" | ASCII_ALPHANUMERIC)+ }

preprocessorDirective = { "#" ~ preprocContent* }
preprocContent = _{ !("\n" | "\r") ~ ANY }

enumEntry = { identifier ~ ("=" ~ int)? }
enum = { "enum" ~ identifier ~ "{" ~ (enumEntry ~ ("," ~ enumEntry)*)? ~ "}" }
anonEnum = { "enum" ~ "{" ~ (enumEntry ~ ("," ~ enumEntry)*)? ~ "}" }

union = { "union" ~ identifier ~ "{" ~ structInner* ~ "}" }
anonUnion = { "union" ~ "{" ~ structInner* ~ "}" }

module = { "module" ~ identifier ~ "{" ~ moduleMember* ~ "}" }
moduleMember = { topLevelWithSemicolon | function }

typedef = { "typedef" ~ typedefInner ~ identifier }
typedefInner = _{ anonStruct | anonEnum | anonUnion | struct | enum | union | typ }

// == Core statements ==
statementWithoutSemicolon = _{ varDec | varAssign | incDec | binOpVarAssign | 
    return | emptyReturn | continue | break | expression | doWhile }
bigStatement = _{ if | while | for | switch }
statement = { (statementWithoutSemicolon ~ ";") | bigStatement }
varAssign = { identExpr ~ "=" ~ expression }
binOpVarAssign = { identExpr ~ arithLogicBinOp ~ "=" ~ expression }
return = { "return" ~ expression }
emptyReturn = { "return" }
continue = { "continue" }
break = { "break" }

varModifier = { "long" | "short" | "signed" | "unsigned" |
    "static" | "volatile" | "extern" | "const" }
varDec = { varModifier* ~ typ ~ identifier ~ ("=" ~ expression)? }

incDecOp = { "++" | "--" }
incDec = { identExpr ~ incDecOp }

conditionBody = { "(" ~ expression ~ ")" ~ "{" ~ statement* ~ "}" }

if = { "if" ~ conditionBody ~ elseifs ~ else? }
elseifs = { elseif* }
elseif = { "else" ~ "if" ~ conditionBody }
else = { "else" ~ "{" ~ statement* ~ "}" }

while = { "while" ~ conditionBody }
doWhile = { "do" ~ "{" ~ statement* ~ "}" ~ "while" ~ "(" ~ expression ~ ")" }
for = { "for" ~ "(" ~ statement ~ expression ~ ";" ~ statementWithoutSemicolon ~ ")" ~ "{" ~ statement* ~ "}" }

switch = { "switch" ~ "(" ~ atom ~ ")" ~ "{" ~ case* ~ default? ~ "}" }
case = { "case" ~ atom ~ ":" ~ statement* }
default = { "default" ~ ":" ~ statement* }

// == Expressions ==
atom = { int | long | double | float | boolean | char | string | typeCast | unaryOperation | sizeof | 
    hybridIdent | "(" ~ expression ~ ")" }
exprTail = { callTail | binaryOperationTail | memberAccessTail | pointerAccessTail | indexTail | ternaryTail | "" }
expression = { atom ~ exprTail }

sizeof = { "sizeof(" ~ typ ~ ")" }

moduleIdent = { identifier ~ "::" ~ identifier }
hybridIdent = { moduleIdent | identifier }

// This setup allows us to require that idenifiers can only start with keywords if
// there are additional characters after
keyword = _{ "return" | "struct" | "true" | "false" | "typedef" | "if" | "while" | "for" | "do" |
    varModifier | "continue" | "break" | "sizeof" | "module" | "typedef" | "union" | "enum" }
identifier = ${ keyword? ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

typ = { ("struct" | "enum" | "union")? ~ hybridIdent ~ "*"* }

typeCast = { "(" ~ typ ~ ")" ~ expression }

int = @{ ("+" | "-")? ~ (ASCII_DIGIT)+ }
long = @{ ("+" | "-")? ~ (ASCII_DIGIT)+ ~ "L" }
double = @{ ("+" | "-")? ~ (ASCII_DIGIT)+ ~ "." ~ (ASCII_DIGIT)+ }
float = @{ ("+" | "-")? ~ (ASCII_DIGIT)+ ~ ("." ~ (ASCII_DIGIT)+)? ~ "f" }
boolean = { "true" | "false" }

char = { "'" ~ chchar ~ "'" }
chchar = {
    !("\'" | "\\") ~ ANY
    | "\\" ~ ("\'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | "0")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

string = ${ "\"" ~ strinner ~ "\"" }
strinner = @{ strchar* }
strchar = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

// Other utils
arithLogicBinOp = { "+" | "-" | "*" | "/" | "%" | "&" | "|" | "^" | "&&" | "||" | "<<" | ">>" }
binOp = { "+" | "-" | "*" | "/" | "%" | "&" | "|" | "^" | "&&" | "||" | "<<" | ">>" | "==" | "!=" | ">" | "<" | ">=" | "<=" }

unaryOp = { "++" | "--" | "+" | "-" | "~" | "!" | "&" | "*" }
unaryOperation = { unaryOp ~ expression }

exprList = _{ (expression ~ ",")* ~ expression? }

parameter = { typ ~ identifier }
paramList = { (parameter ~ ",")* ~ parameter }

pointerIdent = { "*" ~ expression }
identExpr = { identifier | pointerIdent }

// Tails
binaryOperationTail = { binOp ~ expression ~ exprTail }
indexTail = { "[" ~ expression ~ "]" ~ exprTail }
memberAccessTail = { "." ~ identifier ~ exprTail }
pointerAccessTail = { "->" ~ identifier ~ exprTail }
callTail = { "(" ~ exprList ~ ")" ~ exprTail }
ternaryTail = { "?" ~ expression ~ ":" ~ expression ~ exprTail }
